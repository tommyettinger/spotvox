apply plugin: 'application'

java.sourceCompatibility = 17
application.mainClass = 'com.github.tommyettinger.headless.HeadlessLauncher'
eclipse.project.name = appName + '-headless'

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation 'info.picocli:picocli:4.7.7'
	annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
}
compileJava.options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]

jar {
	archiveFileName.set(appName+".jar")
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest {
		attributes (
			'Main-Class' : application.mainClass, 'Enable-Native-Access': 'ALL-UNNAMED'
		)
	}
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}
